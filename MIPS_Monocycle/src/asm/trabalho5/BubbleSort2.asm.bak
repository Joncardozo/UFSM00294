# Programa: BubbleSort
# Descrição: Ordenação crescente

.globl BubbleSort

.text
BubbleSort:
    lw      $t9, arrayc        # load number of arrays
    la      $t0, array         # pointer to first array
    la      $t6, size          # pointer to size array    
    
outer_loop:
    beq     $t9, $zero, end    # if no more arrays, finish
    addiu   $t8, $zero, 1      # t8 = 1: swap performed

while:
    beq     $t8, $zero, next_array  # if no swap, go to next array
    lw      $t3, 0($t6)        # t3 = size of current array
    addiu   $t8, $zero, 0      # reset swap flag
    move    $t4, $t0           # t4 = pointer to start of array
    addiu   $t5, $zero, 0      # t5 = 0 (index)

inner_loop:
    addiu   $t7, $t3, -1       # t7 = size - 1 (last valid index)
    slt     $t1, $t5, $t7      # if index < size-1
    beq     $t1, $zero, while  # exit if we've reached the end
    lw      $t1, 0($t4)        # t1 = array[i]
    lw      $t2, 4($t4)        # t2 = array[i+1]
    slt     $t7, $t2, $t1      # if array[i+1] < array[i]
    beq     $t7, $zero, continue

    # Inline swap
    sw      $t2, 0($t4)        # array[i] = array[i+1]
    sw      $t1, 4($t4)        # array[i+1] = array[i]
    addiu   $t8, $zero, 1      # set swap flag

continue:
    addiu   $t4, $t4, 4        # move pointer to next element
    addiu   $t5, $t5, 1        # index++
    j       inner_loop

next_array:
    addiu   $t9, $t9, -1       # arrayc--
    addiu   $t0, $t0, 200      # move to next array (fixed 50*4 bytes)
    addiu   $t6, $t6, 4        # move to next size
    j       outer_loop

end: 
    jr      $ra

.data 
    array:      .word 12, 29, 17, 46, 33, 23, 21, 40, 6, 16, 33, 16, 18, 30, 38, 19, 30, 43, 5, 32, 3, 30, 26, 29, 40, 9, 22, 25, 3, 50, 27, 47, 50, 21, 20, 0, 10, 0, 17, 21, 37, 5, 3, 30, 37, 13, 13, 10, 31, 30, 36, 35, 23, 9, 1, 45, 20, 30, 37, 12, 11, 50, 20, 20, 44, 45, 11, 13, 21, 50, 35, 48, 26, 28, 31, 39, 24, 2, 2, 9, 14, 13, 15, 42, 36, 16, 50, 6, 43, 1, 9, 0, 44, 7, 20, 49, 6, 5, 33, 49
    size:       .word 50, 50
    arrayc:     .word 2